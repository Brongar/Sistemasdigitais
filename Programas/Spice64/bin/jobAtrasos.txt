*Andrew Flores Brongar - 116500

.include 32nm_HP.pm

* Declarando Fontes de tensão
Vvdd vdd gnd 1

* Declaração das fontes

*PWLs de validação
*Va a gnd PWL (0ns 0 8ns 0 8.01ns 1 16ns 1)
*Vb b gnd PWL (0ns 0 4ns 0 4.01ns 1 8ns 1 8.01ns 0 12ns 0 12.01ns 1 16ns 1)
*Vc c gnd PWL (0ns 0 2ns 0 2.01ns 1 4ns 1 4.01ns 0 6ns 0 6.01ns 1 8ns 1 8.01ns 0 10ns 0 10.01ns 1 12ns 1 12.01ns 0 14ns 0 14.01ns 1 16ns 1)


*novos estímulos - p/ atrasos
Va a gnd PWL (0ns 1 6ns 1 6.01ns 0 8ns 0 8.01ns 1 14ns 1 14.01ns 0 16ns 0 16.01ns 1 20ns 1 20.01ns 0 22ns 0 22.01ns 1 24ns 1)

Vb b gnd PWL (0ns 1 2ns 1 2.01ns 0 12ns 0 12.01ns 1 24ns 1)

Vc c gnd PWL (0ns 0 4ns 0 4.01ns 1 10ns 1 10.01ns 0 18ns 0 18.01ns 1 24ns 1)


* Declarando o circuito - Transistores

*A largura(W) de um transistor CMOS afeta principalmente sua capacidade de condução de corrente e sua capacidade de chaveamento.
*Se dobramos a largura em duas vezes, teoricamente, a resistencia diminui pela metade.
*O comprimento(L), tambem influencia na resistencia, se aumentarmos essa variavel aumentará a resistencia do transistor.
*ao aumentar o comprimento do transistor, também pode haver um aumento no atraso de propagação do sinal através do dispositivo

*Alguns atrasos obtidos antes da modificação (W=100n):
*delay_a_HL1 =  6.623702e-12
*delay_a_LH1 =  8.344814e-12
*delay_b_HL1 =  1.543130e-11 
*delay_b_LH1 =  1.164937e-11 
*delay_c_LH1 =  9.981784e-12
*delay_c_HL1 =  1.383011e-11

*Atrasos obtidos alterando o valor de W (W=2000n) na primeira linha do PMOS:
*delay_a_hl1         =  3.481312e-12
*delay_a_lh1         =  2.778050e-11
*delay_b_hl1         =  3.701306e-11
*delay_b_lh1         =  2.530184e-11
*delay_c_lh1         =  2.469915e-11
*delay_c_hl1         =  3.507741e-11

*Tivemos uma diminuição no primeiro atraso, de 0.006ns para  0.003ns, porém, nos outros atrasos houve um aumento consideravel

*Atraso de propagação: O tamanho dos transistores afeta diretamente a resistência e a capacitância do dispositivo, que são fatores-chave no atraso de propagação.

*Resistência: Transistores maiores geralmente possuem uma resistência mais baixa, o que resulta em uma queda de tensão menor e, portanto, em um atraso de propagação reduzido.

*Capacitância: Transistores maiores possuem uma área de canal maior, resultando em uma capacitância de carga maior. 
*Isso pode aumentar o tempo necessário para carregar e descarregar as capacitâncias e, consequentemente, aumentar o atraso de propagação.

*PMOS
Map1 vdd a inv vdd PMOS W=2000n L=32n
Map2 vdd inv out vdd PMOS W=100n L=32n

Mbp1 vdd b w vdd PMOS W=100n L=32n
Mbp2 w c inv vdd PMOS W=100n L=32n


*NMOS
Mbn1 gnd b z gnd NMOS W=100n L=32n
Mbn2 z a inv gnd NMOS W=100n L=32n
Mbn3 out inv gnd gnd NMOS W=100n L=32n

Mcn1 gnd c z gnd NMOS W=100n L=32n

*Atrasos

*Atrasos na entrada "A"
.measure tran delay_a_HL1 trig v(a) val='0.5*1.0' fall=1 targ v(out) val='0.5*1.0' fall=2
.measure tran delay_a_LH1 trig v(a) val='0.5*1.0' rise=1 targ v(out) val='0.5*1.0' rise=2

.measure tran delay_a_HL2 trig v(a) val='0.5*1.0' fall=2 targ v(out) val='0.5*1.0' fall=4
.measure tran delay_a_LH2 trig v(a) val='0.5*1.0' rise=2 targ v(out) val='0.5*1.0' rise=4

.measure tran delay_a_HL3 trig v(a) val='0.5*1.0'  fall=3 targ v(out)  val='0.5*1.0' fall=5
.measure tran delay_a_LH3 trig v(a) val='0.5*1.0'  rise=3 targ v(out) val='0.5*1.0' rise=5


*Atrasos na entrada "B"
.measure tran delay_b_HL1 trig v(b) val='0.5*1.0' fall=1 targ v(out) val='0.5*1.0' fall=1
.measure tran delay_b_LH1 trig v(b) val='0.5*1.0' rise=1 targ v(out) val='0.5*1.0' rise=3


*Atrasos na entrada "C"
.measure tran delay_c_LH1 trig v(c) val='0.5*1.0' rise=1 targ v(out) val='0.5*1.0' rise=1
.measure tran delay_c_HL1 trig v(c) val='0.5*1.0' fall=1 targ v(out) val='0.5*1.0' fall=3



* Simulação Transiente de 24ns com passo de 0.01ns
.tran 0.02ns 24ns

* Fim do Arquivo SPICE
.end